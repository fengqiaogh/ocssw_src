####################################################
# This script assumes the OCSSWROOT is already set #
####################################################

#
# C Compiler (if not already set)
# gcc, icc
#
if (! $?CC) then
   setenv CC gcc
endif

#
# C plus plus Compiler (if not already set)
# g++, icpc
#
if (! $?CXX) then
   setenv CXX g++
endif

#
# default Fortran Compiler. (if not already set)
#
# ifort, gfortran
#
if (! $?FC) then
   setenv FC gfortran
endif

#
# figure out what we are running on
#
# default to linux
setenv GCC_TUNE ""
setenv EOS_LIB_PREFIX  linux
setenv OCSSW_ARCH  linux
if ( ! $?OCSSW_DEBUG ) then
   setenv OCSSW_DEBUG 0
endif

if (`uname -s | grep Darwin` != '') then
  #
  # MacOS X
  #
  if ( `uname -m | grep ppc` != '') then
    setenv EOS_LIB_PREFIX  macintosh
    setenv OCSSW_ARCH  macosx_ppc
  else
    setenv EOS_LIB_PREFIX  macintel64
    setenv OCSSW_ARCH  macosx_intel
  endif

else
  #
  # Linux
  #
  if ( `uname -m | grep x86_64` != '') then
    setenv EOS_LIB_PREFIX  linux64
    setenv OCSSW_ARCH linux_64
  else
    setenv EOS_LIB_PREFIX  linux
    setenv OCSSW_ARCH linux
  endif
endif

#
# setup lib3 directory
#
setenv LIB3_DIR  ${OCSSWROOT}/opt
setenv LIB3_INC  ${LIB3_DIR}/include
setenv LIB3_LIB  ${LIB3_DIR}/lib
setenv LIB3_CHECK 0
setenv LIB3_BIN ${LIB3_DIR}/bin

# Third-party libraries
setenv PGSLIB      $LIB3_DIR/EOS/lib/$EOS_LIB_PREFIX
setenv PGSINC      $LIB3_DIR/EOS/include
setenv HDFEOS_LIB  $LIB3_DIR/EOS/lib/$EOS_LIB_PREFIX

# data tree
setenv OCDATAROOT   $OCSSWROOT/share

# var tree
setenv OCVARROOT    $OCSSWROOT/var
 
# SWl01
setenv NAVCTL       $OCDATAROOT/seawifs/nav
setenv NAVQC        $OCDATAROOT/seawifs/nav
setenv ORBCTL       $OCDATAROOT/seawifs/nav
setenv ELEMENTS     $OCVARROOT/seawifs
setenv ASAP_PARMS   $OCDATAROOT/common/nav
setenv HRPT_STATION_IDENTIFICATION_FILE $OCDATAROOT/seawifs/station/station_id.dat
 
# OCl1bgen
setenv OCTS_REGISTRATION_TABLES $OCDATAROOT/octs/nav
 
# seawifs format checker
setenv SWTBL $OCDATAROOT/seawifs
setenv SWFTBL $OCDATAROOT/seawifs/qc

# bin directory
setenv OCSSW_BIN $OCSSWROOT/bin

# Ancillary data directory
setenv L2GEN_ANC $OCVARROOT/anc

# executable path
set path=($OCSSW_BIN $OCSSWROOT/scripts $LIB3_BIN $path)
setenv PATH `echo $PATH | tr ':' '\n' | perl -e 'while (<>) { print $_ unless $s{$_}++; }' | tr '\n' ':' | sed 's/::/:/g'| sed 's/:$//'` 

# set module python path
if ( ! $?PYTHONPATH) then
    setenv PYTHONPATH $OCSSWROOT/scripts/:$OCSSWROOT/scripts/modules
else
    setenv PYTHONPATH $OCSSWROOT/scripts/:$OCSSWROOT/scripts/modules:$PYTHONPATH
endif

# setup proj.4 data directory
setenv PROJ_LIB $LIB3_DIR/share/proj

# setup the svn host for direct access or tunneling
#
ping -q -c 1 svn101.domain.sdps >& /dev/null
if ( $? == 0 ) then
  setenv OCSSW_SVN svn101.domain.sdps
else
  setenv OCSSW_SVN localhost
endif
